import java.util.Properties

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    id("com.jaredsburrows.license")
}

/**
 * Короткий хеш коммита, из которого собрано приложение. Показывается в «Информации»,
 * чтобы по установленной сборке было видно, что именно в ней есть.
 * Сборка из архива без .git не должна падать - тогда просто «unknown».
 */
val gitCommit: String = runCatching {
    providers.exec {
        commandLine("git", "rev-parse", "--short", "HEAD")
    }.standardOutput.asText.get().trim()
}.getOrNull()?.takeIf { it.isNotEmpty() } ?: "unknown"

/**
 * Ключ подписи релиза. Берётся из переменных окружения (так его передаёт CI)
 * или из keystore.properties рядом с проектом; в репозитории ни того, ни другого нет.
 *
 * Без ключа релиз собирается неподписанным: локальная сборка не должна падать
 * из-за того, что у разработчика нет боевого ключа.
 */
val keystoreProperties = Properties().apply {
    val file = rootProject.file("keystore.properties")
    if (file.exists()) file.inputStream().use { load(it) }
}

fun signingSecret(name: String): String? =
    (System.getenv(name) ?: keystoreProperties.getProperty(name))?.takeIf { it.isNotBlank() }

val releaseKeystore = signingSecret("WARD_KEYSTORE_FILE")?.let { rootProject.file(it) }
    ?.takeIf { it.exists() }

android {
    namespace = "com.v2ray.ang"
    compileSdk = 37

    // Версию NDK задаёт сборочная среда; локально берётся та, что стоит в SDK.
    // Раньше эту строку вклеивал sed по номеру строки - любая правка шапки файла
    // роняла сборку с «Unresolved reference 'ndkVersion'»
    System.getenv("WARD_NDK_VERSION")?.takeIf { it.isNotBlank() }?.let { ndkVersion = it }

    defaultConfig {
        applicationId = "com.ward.client"
        minSdk = 24
        targetSdk = 37
        versionCode = 756
        // Без суффиксов вида -beta: строка уходит в User-Agent подписок и в сравнение
        // версий при проверке обновлений. Что сборка бета - помечается самим релизом
        versionName = "0.9.15"

        buildConfigField("String", "GIT_COMMIT", "\"$gitCommit\"")

        val abiFilterList = (properties["ABI_FILTERS"] as? String)?.split(';')
        splits {
            abi {
                isEnable = true
                reset()
                if (!abiFilterList.isNullOrEmpty()) {
                    include(*abiFilterList.toTypedArray())
                } else {
                    include(
                        "arm64-v8a",
                        "armeabi-v7a",
                        "x86_64",
                        "x86"
                    )
                }
                isUniversalApk = abiFilterList.isNullOrEmpty()
            }
        }

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if (releaseKeystore != null) {
            create("release") {
                storeFile = releaseKeystore
                storePassword = signingSecret("WARD_KEYSTORE_PASSWORD")
                keyAlias = signingSecret("WARD_KEY_ALIAS") ?: "ward"
                // В PKCS12 отдельного пароля ключа нет, он совпадает с паролем
                // хранилища - поэтому задавать его вторым секретом незачем
                keyPassword = signingSecret("WARD_KEY_PASSWORD")
                    ?: signingSecret("WARD_KEYSTORE_PASSWORD")
                enableV1Signing = true
                enableV2Signing = true
            }
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.findByName("release")
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }

    flavorDimensions.add("distribution")
    productFlavors {
        create("fdroid") {
            dimension = "distribution"
            applicationIdSuffix = ".fdroid"
            buildConfigField("String", "DISTRIBUTION", "\"F-Droid\"")
        }
        create("github") {
            dimension = "distribution"
            buildConfigField("String", "DISTRIBUTION", "\"GitHub\"")
        }
    }

    sourceSets {
        getByName("main") {
            jniLibs.srcDirs("libs")
        }
    }

    compileOptions {
        isCoreLibraryDesugaringEnabled = true
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlin {
        compilerOptions {
            jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17)
        }
    }

    applicationVariants.all {
        val variant = this
        val isFdroid = variant.productFlavors.any { it.name == "fdroid" }
        if (isFdroid) {
            val versionCodes =
                mapOf(
                    "armeabi-v7a" to 2, "arm64-v8a" to 1, "x86" to 4, "x86_64" to 3, "universal" to 0
                )

            variant.outputs
                .map { it as com.android.build.gradle.internal.api.ApkVariantOutputImpl }
                .forEach { output ->
                    val abi = output.getFilter("ABI") ?: "universal"
                    output.outputFileName = "Ward_${variant.versionName}-fdroid_${abi}.apk"
                    if (versionCodes.containsKey(abi)) {
                        output.versionCodeOverride =
                            (100 * variant.versionCode + versionCodes[abi]!!).plus(5000000)
                    } else {
                        return@forEach
                    }
                }
        } else {
            val versionCodes =
                mapOf("armeabi-v7a" to 4, "arm64-v8a" to 4, "x86" to 4, "x86_64" to 4, "universal" to 4)

            variant.outputs
                .map { it as com.android.build.gradle.internal.api.ApkVariantOutputImpl }
                .forEach { output ->
                    val abi = if (output.getFilter("ABI") != null)
                        output.getFilter("ABI")
                    else
                        "universal"

                    output.outputFileName = "Ward_${variant.versionName}_${abi}.apk"
                    if (versionCodes.containsKey(abi)) {
                        output.versionCodeOverride =
                            (1000000 * versionCodes[abi]!!).plus(variant.versionCode)
                    } else {
                        return@forEach
                    }
                }
        }
    }

    androidResources {
        // Приложение говорит на двух языках, а библиотеки тащат свои переводы на
        // десятки. Без этого на системном вьетнамском кнопки Material были бы
        // вьетнамскими, а всё наше - английским: язык вперемешку хуже одного чужого.
        // Заодно из APK уходят полсотни ненужных наборов строк
        localeFilters += listOf("en", "ru")
    }

    buildFeatures {
        buildConfig = true
        compose = true
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
        }
    }

}

dependencies {
    // Core Libraries
    implementation(fileTree(mapOf("dir" to "libs", "include" to listOf("*.aar", "*.jar"))))

    // AndroidX Core Libraries
    implementation(libs.androidx.core.ktx)

    // Compose Libraries
    implementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(platform(libs.androidx.compose.bom))

    implementation(libs.androidx.activity.compose)
    implementation(libs.androidx.compose.ui)
    implementation(libs.androidx.compose.foundation)
    implementation(libs.androidx.compose.material3)
    implementation(libs.androidx.compose.ui.tooling.preview)

    // Жидкое стекло: линза, блики и тени под неё. Apache 2.0, условия в
    // THIRD_PARTY.md. Высокоуровневых компонентов библиотека не публикует - они
    // лежат в её каталоге примеров и перенесены к нам в com.kyant.backdrop.catalog
    implementation(libs.backdrop.android)
    // Формы (Capsule) для перенесённых компонентов. Backdrop объявляет их со
    // scope runtime, на компиляцию такая зависимость не попадает - отсюда и нужно
    // прописать её отдельно
    implementation(libs.kyant.shapes.android)
    implementation(libs.lifecycle.runtime.compose)

    debugImplementation(libs.androidx.compose.ui.tooling)

    // Data and Storage Libraries
    implementation(libs.mmkv.static)
    implementation(libs.gson)
    implementation(libs.okhttp)

    // Reactive and Utility Libraries
    implementation(libs.kotlinx.coroutines.android)
    implementation(libs.kotlinx.coroutines.core)

    // QR Code: CameraX + ZXing
    implementation(libs.camerax.core)
    implementation(libs.camerax.camera2)
    implementation(libs.camerax.lifecycle)
    implementation(libs.camerax.compose)
    implementation(libs.core) // zxing core

    // AndroidX Lifecycle and Architecture Components
    implementation(libs.lifecycle.viewmodel.ktx)
    implementation(libs.lifecycle.runtime.ktx)

    // Background Task Libraries
    implementation(libs.work.runtime.ktx)
    implementation(libs.work.multiprocess)

    // Reorderable list
    implementation(libs.reorderable)

    // Testing Libraries
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    testImplementation(libs.org.mockito.mockito.inline)
    testImplementation(libs.mockito.kotlin)
    coreLibraryDesugaring(libs.desugar.jdk.libs)
}
